## Credentials
resource "seqera_google_credential" "gcp_credential" {
  name         = "gcp_credential"
  workspace_id = var.workspace_id

  # Flattened Google Cloud credential structure
  service_account_key = var.service_account_key
}

# Legacy compute environment resource (for backwards compatibility testing)
resource "seqera_compute_env" "gcp_batch_compute_env_legacy" {
  workspace_id = var.workspace_id

  compute_env = {
    name           = "gcp-batch-compute-env-legacy"
    description    = "GCP Batch compute environment using legacy resource (for testing)"
    platform       = "google-batch"
    credentials_id = resource.seqera_google_credential.gcp_credential.credentials_id

    config = {
      google_batch = {
        region   = "us-central1"
        work_dir = var.work_dir
        location = "us-central1"
      }
    }
  }
}

# New dedicated Google Batch compute environment resource
resource "seqera_google_batch_ce" "gcp_batch_compute_env" {
  name           = "gcp-batch-compute-env"
  workspace_id   = var.workspace_id
  description    = "GCP Batch compute environment for bioinformatics workflows"
  credentials_id = resource.seqera_google_credential.gcp_credential.credentials_id
  # platform defaults to "google-batch" - no need to specify

  config = {
    location          = "us-central1"
    work_dir          = var.work_dir
    boot_disk_size_gb = 50
    spot              = true
    enable_wave       = true
    enable_fusion     = true
  }
}

resource "seqera_pipeline" "hello_world_minimal" {
  description = "Hello world pipeline generated by terraform"
  name        = "terraform-hello-world-1"
  launch = {
    compute_env_id  = resource.seqera_google_batch_ce.gcp_batch_compute_env.id
    config_profiles = []
    head_job_cpus   = 6
    # head_job_memory_mb = 32
    pipeline    = "https://github.com/nextflow-io/hello"
    pull_latest = true
    resume      = false
    revision    = "master"
    work_dir    = var.work_dir
  }
  workspace_id = var.workspace_id
}

## Launch a workflow
resource "seqera_workflows" "my_workflows" {
  compute_env_id = resource.seqera_google_batch_ce.gcp_batch_compute_env.id
  pipeline       = "nextflow-io/hello"
  work_dir       = var.work_dir
  workspace_id   = var.workspace_id
  depends_on     = [seqera_google_batch_ce.gcp_batch_compute_env]
}
