resource "seqera_orgs" "my_org" {
  description = "Example org for running AWS centric workflows"
  full_name   = "aws-example-org"
  name        = "aws-example-org"
}

resource "seqera_workspace" "my_workspace" {
  description = "An example aws workspace created with Terraform"
  name        = "example-workspace-aws"
  full_name   = "Example AWS Workspace"
  org_id      = resource.seqera_orgs.my_org.org_id
  visibility  = "PRIVATE"
}

resource "seqera_datasets" "my_datasets" {
  description  = "Example dataset"
  name         = "terraform-dataset"
  workspace_id = resource.seqera_workspace.my_workspace.id
}

resource "seqera_pipeline_secret" "my_pipelinesecret" {
  name         = "example_terraform_secret"
  value        = "SECRET_VALUE"
  workspace_id = resource.seqera_workspace.my_workspace.id
}

resource "seqera_teams" "my_teams" {
  description = "Team created by Terraform"
  name        = "terraform-example-team"
  org_id      = resource.seqera_orgs.my_org.org_id
}

resource "seqera_credential" "aws_credential" {
  name          = "aws-terraform-credential"
  provider_type = "aws"
  workspace_id  = resource.seqera_workspace.my_workspace.id
  description   = "AWS credentials generated by Terraform"
  keys = {
    aws = {
      access_key      = var.access_key
      secret_key      = var.secret_key
      assume_role_arn = var.iam_role
    }
  }
}

resource "seqera_compute_env" "aws_batch_compute_env" {
  workspace_id = resource.seqera_workspace.my_workspace.id

  compute_env = {
    name           = "aws-batch-compute-env"
    description    = "AWS Batch compute environment for bioinformatics workflows"
    platform       = "aws-batch"
    credentials_id = resource.seqera_credential.aws_credential.credentials_id

    config = {
      aws_batch = {
        discriminator = "aws-batch"
        region        = var.aws_region
        work_dir      = var.work_dir

        # Head job configuration
        head_job_cpus      = 2
        head_job_memory_mb = 4096

        # Features
        fusion2_enabled = true
        wave_enabled    = true

        # Forge configuration 
        forge = {
          dispose_on_deletion = true
          type                = "EC2"
          alloc_strategy      = "BEST_FIT_PROGRESSIVE"

          # Instance configuration
          instance_types = ["m5.large", "m5.xlarge", "m5.2xlarge"]
          min_cpus       = 0
          max_cpus       = 1000


          ebs_auto_scale = false
          gpu_enabled    = false
          arm64_enabled  = false

        }

        # Pre and post-run scripts
        pre_run_script  = file("${path.module}/scripts/pre-run.sh")
        post_run_script = file("${path.module}/scripts/post-run.sh")
      }
    }
  }
}

resource "seqera_data_link" "my_datalink" {
  credentials_id    = resource.seqera_credential.aws_credential.credentials_id
  description       = "data link created by Terraform"
  name              = "terraform-aws-datalink"
  provider_type     = "aws"
  public_accessible = false
  type              = "bucket"
  workspace_id      = resource.seqera_workspace.my_workspace.id
  resource_ref      = var.work_dir
}

resource "seqera_action" "my_action" {
  launch = {
    compute_env_id  = resource.seqera_compute_env.aws_batch_compute_env.compute_env_id
    config_profiles = []
    config_text     = ""
    pipeline        = "https://github.com/nf-core/sarek"
    pre_run_script  = file("${path.module}/scripts/pre-run.sh")
    post_run_script = file("${path.module}/scripts/post-run.sh")

    pull_latest = true
    resume      = true
    revision    = "master"
    work_dir = var.work_dir
  }
  name         = "terraform-hello-world-action"
  workspace_id = resource.seqera_workspace.my_workspace.id
  source       = "tower"
}

resource "seqera_pipeline" "rnaseq_pipeline" {
  description = "RNA-seq analysis pipeline using nf-core/rnaseq"
  name        = "rna-seq"
  icon        = "https://avatars.githubusercontent.com/u/35520196?v=4"
  label_ids   = [resource.seqera_labels.my_labels.label_id]
  launch = {
    compute_env_id      = resource.seqera_compute_env.aws_batch_compute_env.compute_env_id
    config_profiles     = ["test_full"]
    pipeline            = "https://github.com/nf-core/rnaseq"
    revision            = "3.19.0"
    work_dir            = var.work_dir
    params_text         = "{\"outdir\": \"${var.work_dir}\"}"
    pull_latest         = false
    stub_run            = false
    workspace_secrets   = [resource.seqera_pipeline_secret.my_pipelinesecret.name]
    pre_run_script      = file("${path.module}/scripts/pre-run.sh")
    post_run_script     = file("${path.module}/scripts/post-run.sh")
  }

  workspace_id = resource.seqera_workspace.my_workspace.id
}

resource "seqera_workflows" "rnaseq_workflow" {
  compute_env_id      = resource.seqera_compute_env.aws_batch_compute_env.compute_env_id
  pipeline            = "nf-core/rnaseq"
  revision            = "3.19.0"
  work_dir            = var.work_dir
  workspace_id        = resource.seqera_workspace.my_workspace.id
  config_profiles     = ["test_full"]
  params_text         = "{\"outdir\": \"${var.work_dir}\"}"
  pull_latest         = false
  stub_run            = false
  label_ids           = [resource.seqera_labels.my_labels.label_id]
  workspace_secrets   = [resource.seqera_pipeline_secret.my_pipelinesecret.name]
  pre_run_script      = file("${path.module}/scripts/pre-run.sh")
  post_run_script     = file("${path.module}/scripts/post-run.sh")
  depends_on          = [seqera_compute_env.aws_batch_compute_env]
}

resource "seqera_studios" "my_datastudios" {
  compute_env_id = resource.seqera_compute_env.aws_batch_compute_env.compute_env_id
  description    = "Data studio"
  name           = "Terraform-Data-Studio"
  configuration = {}
  workspace_id         = resource.seqera_workspace.my_workspace.id
  data_studio_tool_url = "public.cr.seqera.io/platform/data-studio-jupyter:4.2.5-0.8"
  depends_on           = [seqera_compute_env.aws_batch_compute_env]
}

resource "seqera_labels" "my_labels" {
  is_default   = false
  name         = "terraform-example-label"
  resource     = true
  value        = "terraform-label-value"
  workspace_id = resource.seqera_workspace.my_workspace.id
}
